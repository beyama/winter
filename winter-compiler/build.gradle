plugins {
    id 'kotlin'
    id 'org.jetbrains.dokka'
    id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC9.2'
}

version = VERSION_NAME
group = GROUP

dependencies {
    implementation project(':winter')

    implementation deps.kotlin.stdlib

    implementation deps.javaxInject
    implementation deps.kotlinpoet

    testImplementation deps.junit.jupiterApi
    testImplementation deps.kotlintest
    testImplementation "com.google.testing.compile:compile-testing:0.15"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includes = ['dokka-packages.md']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/beyama/winter/blob/master/winter/src/main/kotlin"
        suffix = "#L"
    }
}

detekt {
    config = files(rootProject.file('detekt-config.yml'))
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')