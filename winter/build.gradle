plugins {
    id 'kotlin'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
    id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC9.2'
}

kapt {
    arguments {
        arg("winterGeneratedComponentPackage", "io.jentz.winter")
    }
}

version = VERSION_NAME
group = GROUP

sourceSets.test.kotlin.srcDir "${buildDir.absolutePath}/generated/source/kaptKotlin/test"

test {
    useJUnitPlatform()

    dependsOn 'cleanTest'

    testLogging {
        events "skipped", "failed"
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

dependencies {
    implementation deps.kotlin.stdlib

    // JSR330
    kaptTest project(':winter-compiler')
    testImplementation 'javax.inject:javax.inject:1'

    testImplementation deps.junit.junit4
    testImplementation deps.junit.jupiterApi
    testImplementation deps.kotlintest
    testImplementation deps.mockito.core
    testImplementation deps.mockito.kotlin

    testRuntimeOnly deps.junit.jupiterEngine
    testRuntimeOnly deps.junit.vintageEngine
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includes = ['dokka-packages.md']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/beyama/winter/blob/master/winter/src/main/kotlin"
        suffix = "#L"
    }
}

detekt {
    config = files(rootProject.file('detekt-config.yml'))
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')