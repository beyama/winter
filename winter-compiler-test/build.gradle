apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'

kapt {
    arguments {
        arg("winterGeneratedComponentPackage", "io.jentz.winter.compilertest")
        arg("winterPrintSources", "true")
    }
}

sourceSets.main.kotlin.srcDir "${buildDir.absolutePath}/generated/source/kaptKotlin/main"

test {
    useJUnitPlatform()

    dependsOn 'cleanTest'

    testLogging {
        events "skipped", "failed"
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

dependencies {
    implementation project(":winter")

    kapt project(':winter-compiler')

    implementation deps.kotlin.stdlib
    implementation 'javax.inject:javax.inject:1'

    testImplementation deps.junit.junit4
    testImplementation deps.junit.jupiterApi
    testImplementation deps.kotlintest

    testRuntimeOnly deps.junit.jupiterEngine
    testRuntimeOnly deps.junit.vintageEngine
}
